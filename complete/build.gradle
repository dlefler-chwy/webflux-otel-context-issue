import org.springframework.boot.gradle.tasks.bundling.BootJar
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

configurations {
	agent
	extension
}

repositories {
	mavenCentral()
}

dependencies {
	implementation(platform(SpringBootPlugin.BOM_COORDINATES))

	implementation("io.opentelemetry:opentelemetry-api")
	implementation('io.opentelemetry.instrumentation:opentelemetry-spring-webflux-5.3:2.12.0-alpha')

	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'

	agent("io.opentelemetry.javaagent:opentelemetry-javaagent:2.13.3")
	extension("io.opentelemetry.contrib:opentelemetry-samplers:1.44.0-alpha") {
		transitive = false
	}
}

test {
	useJUnitPlatform()
}

tasks.register('copyAgent', Copy) {
	from configurations.agent.singleFile
	into layout.buildDirectory.dir("agent")
	rename 'opentelemetry-javaagent-.*\\.jar', 'opentelemetry-javaagent.jar'
}

tasks.register('copyExtension', Copy) {
	from configurations.extension.singleFile
	into layout.buildDirectory.dir("agent")
	rename '.*\\.jar', 'opentelemetry-javaagent-extension.jar'
}

tasks.named('bootJar', BootJar) {
	dependsOn copyAgent
	dependsOn copyExtension

	archiveFileName = 'app.jar'
}